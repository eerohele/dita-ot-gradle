plugins {
    id 'com.gradle.plugin-publish' version '0.9.8'
    id 'de.undercouch.download' version '3.4.2'
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'codenarc'
apply plugin: 'java-gradle-plugin'

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

defaultTasks 'check'

dependencies {
    compile gradleApi()
    compile 'commons-io:commons-io:1.4'

    testCompile gradleTestKit()

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:19.0'
    testCompile 'xml-resolver:xml-resolver:1.2'
}

repositories {
    jcenter()
}

codenarc {
    toolVersion = '1.0'
    configFile = file('config/codenarc/codenarc.groovy')
}

def ditaOtVersion = '3.2.1'

task downloadDitaOt(type: Download) {
    src "https://github.com/dita-ot/dita-ot/releases/download/${ditaOtVersion}/dita-ot-${ditaOtVersion}.zip"
    dest new File(buildDir, "dita-ot-${ditaOtVersion}.zip")
    overwrite false
}

task extractDitaOt(dependsOn: downloadDitaOt, type: Copy) {
    from zipTree(downloadDitaOt.dest)
    into buildDir
}

task installDitaOt(dependsOn: [extractDitaOt])

test {
    dependsOn installDitaOt
    systemProperty 'dita.home', System.getProperty('dita.home', "${buildDir}/dita-ot-${ditaOtVersion}")
    systemProperty 'examples.dir', new File(projectDir, 'examples')
}

group = 'com.github.eerohele'
version = '0.6.0-beta2'

pluginBundle {
    website = 'https://github.com/eerohele/dita-ot-gradle'
    vcsUrl = 'https://github.com/eerohele/dita-ot-gradle.git'
    description = 'A Gradle plugin for running DITA Open Toolkit'
    tags = ['dita', 'dita-ot', 'documentation']

    plugins {
        DitaOtPlugin {
            id = 'com.github.eerohele.dita-ot-gradle'
            displayName = 'DITA-OT Gradle Plugin'
        }
    }
}

gradlePlugin {
    plugins {
        customPlugin {
            id = 'com.github.eerohele.dita-ot-gradle'
            implementationClass = 'com.github.eerohele.DitaOtPlugin'
        }
    }
}
